HISTFILE=$ZDOTDIR/histfile
HISTSIZE=1000
SAVEHIST=1000
setopt autocd extendedglob no_case_glob dotglob
unsetopt beep
bindkey -v

# Completion
zstyle :compinstall filename "$ZDOTDIR/.zshrc"

zstyle ':completion:*' completer _extensions _complete _approximate
zstyle ':completion:*' menu select
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}

autoload -Uz compinit && compinit -d $ZDOTDIR/.zcompdump

# Fancy colors
[ -e "$XDG_CONFIG_HOME/dircolors" ] && \
  eval "$(dircolors $XDG_CONFIG_HOME/dircolors)"

# Aliases
[ -e "$XDG_CONFIG_HOME/aliasrc" ] && \
  source $XDG_CONFIG_HOME/aliasrc

# Prompt
PROMPT="%F{85}[%n:%f%~%F{85}]%(?.%F{85}.%F{160})%(!.#.$)%f "

# Change cursor with support for inside/outside tmux
export KEYTIMEOUT=1

function _set_cursor() {
    if [[ $TMUX = '' ]]; then
      echo -ne $1
    else
      echo -ne "\ePtmux;\e\e$1\e\\"
    fi
}

function _set_block_cursor() { _set_cursor '\e[2 q' }
function _set_beam_cursor() { _set_cursor '\e[6 q' }

function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] || [[ $1 = 'block' ]]; then
      _set_block_cursor
  else
      _set_beam_cursor
  fi
}
zle -N zle-keymap-select
# Ensure beam cursor when starting new terminal
precmd_functions+=(_set_beam_cursor)
# Ensure insert mode and beam cursor when exiting vim
zle-line-init() { zle -K viins; _set_beam_cursor }

# Syntax highlighting (Has to be last line)
source $ZDOTDIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
